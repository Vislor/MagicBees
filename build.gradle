buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name 'ForgeFS'
        url 'http://files.minecraftforge.net/maven'
    }
    maven { // JEI
        name = "progwml6"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { //IC2
        name = "IC2"
        url = "http://maven.ic2.player.to/"
    }
    maven { // JEI Bees & Thaumcraft
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name "CoFH"
        url "http://maven.covers1624.net/"
    }
    maven { // Baubles
        name "Thiakil"
        url = "http://maven.thiakil.com"
    }
    maven { // Botania
        name "BlameJared"
        url "https://maven.blamejared.com/"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
    project.ext.modVersion = "${config.mod_version}." + (System.env.BUILD_NUMBER ?: "localBuild")
}

ext.modInfo = file "mod.properties"

modInfo.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.mod = new ConfigSlurper().parse prop
}

version = "${config.minecraft_version}-" + modVersion
group= mod.group // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = mod.fileName

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"

    replaceIn mod.mainClass
    replace "#FORGE_VER#", config.forge_version

    mappings = config.mappings

    useDepAts = true
}

dependencies {
    deobfProvided "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}:api"
    runtime "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}"
    deobfCompile("net.sengir.forestry:forestry_${config.forestry_version}") {
        transitive = false
    }
    runtime "jei-bees:jeibees:${config.jeibees_version}:mc${config.minecraft_version}"
    deobfCompile "com.azanor.baubles:Baubles:1.12-${config.baubles_version}"
    deobfCompile "vazkii.botania:Botania:${config.botania_version}"
    deobfCompile ("cofh:ThermalFoundation:${config.minecraft_version}-${config.thermal_foundation_version}:universal") {
        exclude group: 'mezz.jei', module: 'jei_1.12.2'
    }
    deobfCompile "thaumcraft:Thaumcraft:1.12.2:6.1.BETA17"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", modVersion
    inputs.property "mcversion", config.minecraft_version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':modVersion, 'mcversion':config.minecraft_version, 'modid': mod.modid
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes	'FMLAT': 'magicbees_at.cfg'
    }
}
